/*
 * Copyright (C) 2018  Zerthick
 *
 * This file is part of mcSkills.
 *
 * mcSkills is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 2 of the License, or
 * (at your option) any later version.
 *
 * mcSkills is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with mcSkills.  If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id 'org.spongepowered.plugin' version '0.8.1'
    id 'java'
    id "com.qixalite.spongestart2" version "3.1.0"
    id 'maven'
    id 'signing'
}

group 'io.github.zerthick'
version '0.0.1'

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
}

sourceSets {
    api
}

spongestart{
    minecraft '1.12.2'
}

dependencies {
    apiCompile 'org.spongepowered:spongeapi:7.0.0'
    compile 'org.spongepowered:spongeapi:7.0.0'
    compileOnly sourceSets.api.output
}

jar {
    from sourceSets.api.output
}

task apiJar(type: Jar) {
    classifier = 'api'
    from sourceSets.api.output
}

javadoc {
    source = sourceSets.api.java
    options.links("https://docs.oracle.com/javase/8/docs/api/","https://jd.spongepowered.org/7.0.0/")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    from sourceSets.api.allSource
}

artifacts {
    archives apiJar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'mcSkills'
                artifactId 'mcskills'
                packaging 'jar'
                description 'A Minecraft RPG plugin'
                url 'https://github.com/Zerthick/mcSkills'

                scm {
                    connection 'scm:git:https://github.com/Zerthick/mcSkills.git'
                    developerConnection 'scm:git:git@github.com:Zerthick/mcSkills.git'
                    url 'https://github.com/Zerthick/mcSkills'
                }

                issueManagement {
                    system = 'GitHub Issues'
                    url = "https://github.com/Zerthick/mcSkills/issues"
                }

                licenses {
                    license {
                        name 'GNU General Public License v2.0'
                        url 'https://github.com/Zerthick/mcSkills/blob/master/LICENSE.txt'
                    }
                }

                developers {
                    developer {
                        id 'Zerthick'
                        name 'Chase Greco'
                        email 'zerthick@gmail.com'
                    }
                }
            }
        }
    }
}

